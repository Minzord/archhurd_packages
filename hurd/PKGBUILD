# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=hurd
pkgver=0.9.r145.g06429d67
# branch 'master' in hurd/hurd.git
_commit=06429d67b7e9be332d1012ed4c2bba694e5ddabb
pkgrel=1
pkgdesc="The GNU Hurd"
arch=(i686)
url="http://www.gnu.org/software/hurd/hurd.html"
license=('GPL')
groups=('base')
# TODO: Make parted an optdepend (and makedepend probably)
depends=('gnumach' 'glibc' 'parted')  # initscripts
makedepends=('git' 'glibc')
options=('!makeflags')
backup=(etc/ttys)
source=(git+https://git.savannah.gnu.org/git/hurd/hurd.git#commit=$_commit
        ttys)
md5sums=('SKIP'
         '9d20e63e81835c76cc708ce6184f3530')
options=('debug' '!strip')

pkgver() {
  cd "$pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build

  cd $pkgname
  autoreconf -fi
}

build() {
  cd "$srcdir/build"

  # TODO: Remove this?
  export LDFLAGS="$(echo $LDFLAGS | sed 's/-Wl,--as-needed//')"
  
  ../$pkgname/configure \
     --prefix=/usr \
     --sbindir=/usr/bin \
     --libexecdir=/usr/lib \
     --sysconfdir=/etc \
     --disable-profile \
     --enable-static-progs='iso9660fs,ext2fs,ufs'
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir/" install

  install -m 644 $srcdir/ttys $pkgdir/etc/ttys

  # system startup scripts are in the initscripts package
  rm $pkgdir/usr/lib/runsystem $pkgdir/usr/lib/rc

  # for rc.shutdown
  mv $pkgdir/usr/bin/halt $pkgdir/usr/bin/halt-hurd
  mv $pkgdir/usr/bin/reboot $pkgdir/usr/bin/reboot-hurd

  # uptime gets provided by procps-ng
  mv "$pkgdir"/usr/bin/uptime{,-hurd}
  # fsck gets provided by util-linux
  mv "$pkgdir"/usr/bin/fsck{,-hurd}

  # fix install prefix of /dev & /hurd
  mv "$pkgdir"/usr/dev "$pkgdir"/dev
  mv "$pkgdir"/usr/hurd "$pkgdir"/hurd

  # remove files included in hurd-headers
  rm -rf "$pkgdir"/usr/include
}

